#!/usr/bin/env zsh

# required: fzf,jq,fileicon

zparseopts -D -E -F - \
  -process-all=process_all \
  -process-existing=process_existing \
  -favorites=_favorites \
  -history=_history \
  {p,-playlist}:=playlist \
  {t,-template}:=template \
  {h,-help}=help || {
    print "see --help" >&2
    return 1
  }

if [[ -n $help ]]; then
cat <<- --------
usage: rashortcuts [OPTION]... [OUTPUT]

Playlists:
  --favorites         favorites playlist
  --history           history playlist
  -p, --playlist      path/to/playlist.lpl

If a playlist is not provided, fzf will list them automatically.

Process without fzf game selection.
  --process-all       process all entries for a given playlist
  --process-existing  process entries for a given playlist that already exists in
                      the output directory.

Last argument should point to the directory where the shortcut.app will be created.
Defaults to the current working directory.

Custom templates:
  -t, --template      path/to/template-folder

An optional template for building shortcuts. It defaults to the folder named
"template" located in the same directory as this script.

  -h, --help          view help.

--------
return
fi

# Print with style.
p.header(){ printf "\n\e[1;30m%s\e[0m\n" $@ }
p.ok()    { printf   "\e[0;32m%s\e[0m\n" $@ }
p.warn()  { printf   "\e[0;33m%s\e[0m\n" $@ }
p.error() { printf   "\e[0;31m%s\e[0m\n" $@ >&2 }

# Make .icns files.
m.icns() {
  local icnset=${TMPDIR}`uuidgen`.iconset
  mkdir $icnset
  sips -j $this_dir/makeicnset.js $1 -o $icnset
  iconutil -c icns $icnset -o $2
  rm -rf $icnset
}

output=${1:-.}

app_support=~/Library/Application\ Support/RetroArch
this_dir=$(dirname `realpath $0`)

# Ensure the default template is located with this script.
template=${template[-1]:-$this_dir/template}

for f in info.plist MacOS/launcher; [[ -f $template/Contents/$f ]] || {
  p.error "Check template: $f:t file not found." \
          "- $template/Contents/$f"
  return 1
}

if [[ -n $playlist ]]; then
  playlist=${playlist[-1]}
else
  for k in _favorites _history; [[ -n ${(P)k} ]] && {
    content_path=$(eval printf %s $(
      awk -F= "/content${k}_path/ { print \$2 }" $app_support/config/retroarch.cfg
    ))
    playlist=${content_path/#\~/$HOME}
    break
  }
fi

if [[ ! -n $playlist ]]; then
  playlistdir=$(eval printf %s $(
    awk -F= '/playlist_directory/ {print $2}' $app_support/config/retroarch.cfg
  ))
  playlist=$(
    for lpl in ${playlistdir/#\~/$HOME}/*.lpl; do
      print -- "$lpl\t$lpl:t:r"
    done | fzf --delimiter '\t' --with-nth 2 --preview "jq -r '.items[].label' {1}" | cut -f1
  )
  if [[ ! -n $playlist ]]; then
    p.warn "Nothing selected."
    return
  fi
fi

if [[ ! -f $playlist ]]; then
  p.error "Playlist does not exist:" \
          "- $playlist"
  return 1
fi

# json list of cleaned up items.
# Ensure .label and .db_name are present.
items=$(
  jq '.items[] | select(( .label | length > 0 ) and ( .db_name | length > 0 )) | {
    system:.db_name|sub(".lpl"; ""),
    label: .label,
    gpath: .path,
    cname: .core_name,
    cpath: .core_path
  }' $playlist | jq -n '[inputs]'
)

limit=`print -- $items | jq 'length'`
if [[ -n $process_all ]] || [[ -n $process_existing ]]; then
  work_indices=($(
    for (( i=0; i!=$limit; i++ )); print -- $i
  ))
else
  work_indices=($(
    for (( i=0; i!=$limit; i++ )); do
      print -- "$i\t`print -- $items | jq -r ".[$i].label"`"
    done | fzf --multi --delimiter '\t' --with-nth 2 | cut -f1
  ))
fi

if [[ -z $work_indices ]]; then
  p.warn "Nothing to process."
  return
fi

default_cname=`jq -r ".default_core_name" $playlist`
default_cpath=`jq -r ".default_core_path" $playlist`

IFS=$'\n'
for i in $work_indices; do
  read -d '' system label gpath cname cpath < <(
    print -- $items | jq -r ".[$i] | .system, .label, .gpath, .cname, .cpath"
  )
  cname=${${cname/DETECT}:-$default_cname}
  cpath=${${cpath/DETECT}:-$default_cpath}

  if [[ -z $cpath ]]; then
    p.error "Core could not be determined from the playlist. Do one of the following:" \
            "- Set a default core for the playlist. (recommended)" \
            "- Run the game in RetroArch at least once from the playlist."
    continue
  fi

  # Make label file system safe.
  # - replace ':' with '：' [Unicode U+FF1A] and '.'
  # - replace '/' with '᜵'. [Unicode U+1735]
  shortcut_app=$output/${${${label//: /：}//:/.}//\//᜵}.app

  if [[ -n $process_existing ]] && [[ ! -d $shortcut_app ]]; then
    continue
  fi

  # Create the app bundle.
  p.header "Creating \"$shortcut_app:t\""

  mkdir -p $shortcut_app
  if ! cp -R $template/Contents $shortcut_app; then
    p.error "Could not create \"$shortcut_app:t\"."
    continue
  fi

  # gpathextended: full path including possible archive content.
  # gpath: game path without the archive content pointers.
  # fname: name of the file itself.
  # aname: name of archive content. Empty if not present.
  # label: name from playlist name field usually taken from a database.

  unset match
  [[ $gpath:t =~ "(.*\.[[:alnum:]]{2,3})\#(.*)\.[[:alnum:]]{2,4}$" ]]
  gpathextended=$gpath
  gpath=$gpath:h/${match[1]:-$gpath:t}
  fname=$gpath:t:r
  aname=$match[2]

  # short core name
  unset match
  [[ $cpath:t =~ "(.*)_libretro.dylib" ]]
  scname=$match[1]

  # INFO.PLIST: Set bundle id.
  # Limit application bundle id to alphanumerics.
  bundleid=com.libretro
  for s in $scname $fname; bundleid+=.`printf %s $s | tr -cd '[:alnum:]'`
  plutil -insert CFBundleIdentifier -string $bundleid $shortcut_app/Contents/info.plist
  p.ok "BundleID: $bundleid"

  # LAUNCHER: Set system, rom and core paths.
  plutil -insert EmuSystem -string $system $shortcut_app/Contents/info.plist
  plutil -insert EmuROMPath -string $gpathextended $shortcut_app/Contents/info.plist
  plutil -insert EmuCorePath -string $cpath $shortcut_app/Contents/info.plist
  if [[ ! -f $gpath ]]; then
    p.error "\"$gpath:t\" not found. \"$playlist:t\" pointing to a non-existing file:" \
            "- $gpath"
  fi
  if [[ -f $cpath ]]; then
    p.ok "Core: $cname"
  else
    p.error "\"$cpath:t\" not found. \"$playlist:t\" pointing to a non-existing file:" \
            "- $cpath"
  fi

  typeset -aU thumbnames=(
    $fname
    $aname
    $label
  )

  # For each name in $thumbnames:
  # - Chop off each occurance of `(.*)` or `[.*]` from the trailing end.
  # - Add each iteration to the list of naming candidates.
  for _name in $thumbnames; while [[ $_name =~ "^(.+)(\(|\[)([^([]*)(\)|\]).*$" ]]; do
    _name=$match[1]
    # Discard trailing spaces.
    thumbnames+=${_name%${_name##*[! ]}}
  done

  thumbfound=0
  icnpath=$shortcut_app/Contents/Resources/AppIcon.icns
  for thumb in $thumbnames; do
    # Mimic RetroArch's safe naming for thumbnails.
    thumb=`printf %s $thumb | tr '&*/:\`<>?\|' '_'`
    thumbpath=$app_support/thumbnails/$system/Named_Boxarts/$thumb.png
    if [[ -f $thumbpath ]]; then
      thumbfound=1
      m.icns $thumbpath $icnpath
      p.ok "Boxart/$thumb.png -> $icnpath:t"
      break
    fi
  done

  # Use RetroSystem XMB theme content icon as a fallback.
  if ! (( $thumbfound )); then
    fallback=$app_support/assets/xmb/retrosystem/png/$system-content.png
    if [[ -f $fallback ]]; then
      p.warn "retrosystem/$system-content.png -> $icnpath:t"
      m.icns $fallback $icnpath
    else
      p.warn "$icnpath:t not copied."
    fi
  fi

  chmod +x $shortcut_app/Contents/MacOS/launcher

  # Work around Tahoe's icon jail.
  if (( `sw_vers -productVersion` >= 26 )); then
    if fileicon set -q $shortcut_app $icnpath; then
      p.ok "Freed from icon jail"
    fi
  fi

done
